apply plugin: 'groovy'
apply plugin: 'idea'

group = 'kaizen'
version = '0.2.2'

task publish(dependsOn: [test, uploadArchives])

task unityPackage <<{

	def unityExecutable = project.hasProperty('unityExecutable') ?
		project.property('unityExecutable') :
		'/Applications/Unity/Unity.app/Contents/MacOS/Unity'

	def logFile = file("$buildDir/unityPackage.log")
	logFile.parentFile.mkdirs()

	project.exec {
		commandLine unityExecutable
		args '-batchMode'
		args '-quit'
		args '-projectPath'
		args file('templates/kaizen.unityPackage')
		args '-exportPackage'
		args 'Assets/kaizen'
		args file("$buildDir/kaizen.unityPackage")
		args '-cleanedLogFile'
		args logFile
	}
	println logFile.text
}

dependencies {
	compile gradleApi()
	groovy localGroovy()
	testCompile 'org.spockframework:spock-core:0.7-groovy-1.8'
}

uploadArchives {
	repositories {
		ivy { url '../repository' }
	}
}

repositories {
	mavenCentral()
}
