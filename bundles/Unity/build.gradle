apply plugin: 'base'
apply from: '../common.gradle'

if (!hasProperty('unityDir'))
	ext { unityDir = '/Applications/Unity/Unity.app' }

if (!hasProperty('unityLibsDir'))
	ext { unityLibsDir = new File(unityDir, 'Contents/Frameworks/Managed') }

def bundleVersionPattern = ~/<key>CFBundleVersion<\/key>\s*\n\s*<string>(.+)<\/string>/

version = bundleVersionPattern.matcher(new File(unityDir, 'Contents/Info.plist').text).collect { it[1] }.first()

subprojects {

	version = parent.version
	group = 'Unity'

	configurations {
		editor
	}

	apply plugin: 'base'

	task publish(dependsOn: uploadEditor)

	task zip(type: Zip) {
		from unityLibsDir
		include "${project.name}.dll"
	}

	artifacts {
		editor zip
	}

	tasks.uploadEditor {
		repositories {
			ivy { url repositoryForPublishing }
		}
	}
}

def UnityEngine = project(':UnityEngine')
def UnityEditor = project(':UnityEditor')
UnityEditor.dependencies.add(
	'editor',
	UnityEditor.dependencies.project(
			path: UnityEngine.path,
			configuration: 'editor'))
