apply plugin: 'base'
apply from: '../common.gradle'

ext {
	unityDir = '/Applications/Unity/Unity.app'
	unityLibsDir = new File(unityDir, 'Contents/Frameworks/Managed')
}

task init <<{
	def bundleVersionPattern = ~/<key>CFBundleVersion<\/key>\s*\n\s*<string>(.+)<\/string>/
	def version = bundleVersionPattern.matcher(new File(unityDir, 'Contents/Info.plist').text).collect { it[1] }.first()
	subprojects.each { project ->
		project.version = version
		project.group = 'Unity'
	}
}

subprojects {

	configurations {
		editor
	}

	apply plugin: 'base'

	task publish(dependsOn: uploadEditor)

	task zip(type: Zip) {
		from unityLibsDir
		include "${project.name}.dll"
	}

	artifacts {
		editor zip
	}

	tasks.uploadEditor {
		repositories {
			ivy { url repositoryForPublishing }
		}
	}
}

def UnityEngine = project(':UnityEngine')
def UnityEditor = project(':UnityEditor')
UnityEditor.dependencies.add(
	'editor',
	UnityEditor.dependencies.project(
			path: UnityEngine.path,
			configuration: 'editor'))
